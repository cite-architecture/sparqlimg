
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'groovy'
apply plugin: 'maven'

apply from: "versions.gradle"


group = "edu.holycross.shot"
version = "0.5.1"



// Consider possible configurations:
if (hasProperty('conf')) {
    System.err.print "Using configuration data from ${conf}"
    File confFile = new File(conf)
    if (! confFile.exists()) {
        throw new Exception("No configuration file ${conf} found.")
    }
    apply from: conf

} else {
    File confFile = new File("conf.gradle")
    if (! confFile.exists()) {
        throw new Exception("No configuration file ${conf} found.")
    }
    println "Using default configuration in 'conf.gradle'"
    apply from: "conf.gradle"
}


// Configuration of nexus authentication
if (hasProperty('pub')) {
    System.err.print "Using publication configuration from ${pub}"
    File pubConf = new File(pub)
    if (! pubConf.exists()) {
        throw new Exception("No configuration file ${pub} found.")
    }
    apply from: pub
} else {
    // default setting with null string values
    apply from: "pub.gradle"
}


httpPort = 8888
stopPort = 9451
stopKey = 'foo'



repositories {
    mavenCentral()
    // for http builder:
    maven {
        url "http://repository.codehaus.org"
    }
    // for cite libs:
    maven {
        url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
    }
    // for IBM Normalizer
    maven {
        url "https://oss.sonatype.org/content/repositories/releases"
    }
}

configurations {
    publib
}

dependencies {
    groovy  group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion

    compile group: 'commons-io', name: 'commons-io', version: commonsioVersion

    compile ("log4j:log4j:${log4jVersion}") {
        exclude group: "com.sun.jdmk", module: "jmxtools"
        exclude group: "com.sun.jmx", module: "jmxri"
        exclude group: "javax.mail", module: "mail"
        exclude group: "javax.jms", module: "jms"
    }

    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: httpbuilderVersion
    compile group: 'edu.harvard.chs', name : 'cite' , version: citeVersion

    runtime group: 'edu.harvard.chs', name : 'cite' , version: citeVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group :'xmlunit', name: 'xmlunit', version: xmlunitVersion
    testCompile group: 'com.ibm.icu', name: 'icu4j', version: icu4jVersion
}


task copyBuildPkg(type: Copy) {
    from "pkg"
    into "${buildDir}/sparqlimg-template"
    filter(ReplaceTokens, tokens: [sparqlimgversion : version])
}

task copyWeb(type: Copy) {
    from "src/main/webapp"
    into "${buildDir}/sparqlimg-template/src/main/webapp"
}


task buildTemplate(type: Zip, dependsOn: [copyBuildPkg, copyWeb] ) {
    classifier = "template"
    from "${buildDir}/sparqlimg-template"
}


war {
    filter(ReplaceTokens, tokens: [sparqls: sparqls, iipsrv : iipsrv, defaultUrn : defaultUrn, serviceUrl : serviceUrl, detailWidth: detailWidth ])

    classpath sourceSets.main.output.classesDir
    classpath configurations.runtime
}

artifacts {
    publib jar
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

uploadPublib {
  repositories.mavenDeployer {
        repository(url: nexusRepo) {                   
            authentication (userName: nexusUser, password: nexusPassword)
        }
    }
}

uploadPublib.onlyIf { (project.hasProperty('nexusRepo') && project.hasProperty('nexusUser') && project.hasProperty('nexusPassword'))}
